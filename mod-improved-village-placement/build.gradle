plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id "me.modmuss50.mod-publish-plugin" version "0.5.1"
}

base {
    archivesName = "improved_village_placement"
}

processResources {
    from project(":common").sourceSets.main.resources
}

tasks.register('compileDatapack', Zip) {
    archiveFileName = "improved_village_placement_%s.jar".formatted(project.version)
    destinationDirectory = file("$buildDir/distributions")

    from(project(':common').file("src/main/resources/resourcepacks/improved_village_placement")) {
        include "**/*"
    }
}

publishMods {
    file = compileDatapack.archiveFile
    changelog = "Initial release."
    type = STABLE
    modLoaders.add("fabric")
    modLoaders.add("forge")
    modLoaders.add("neoforge")
    displayName = "v%s".formatted(project.version)

    dryRun = false

    modrinth {
        accessToken = providers.environmentVariable("TOKEN_MR")
        projectId = "KA0MllEw"

        minecraftVersions.add("1.20.1")
        minecraftVersions.add("1.21")
        minecraftVersions.add("1.21.1")
        minecraftVersions.add("1.21.4")

        requires {
            slug = "lithostitched"
        }
    }

    curseforge {
        accessToken = providers.environmentVariable("TOKEN_CF")
        projectId = "1175325"

        minecraftVersions.add("1.20.1")
        minecraftVersions.add("1.21")
        minecraftVersions.add("1.21.1")
        minecraftVersions.add("1.21.4")

        requires("lithostitched")
    }
}